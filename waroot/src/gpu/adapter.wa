
import "js"

// Adapter 对象，不能使用该类型直接声明值，需通过 RequestAdapter 创建
type Adapter struct {
    js.ExtObj
}

//---------------------------------------------------------------

// 获取 Device，异步
#wa:generic RequestDevice_Handler
func Adapter.RequestDevice(requestor: DeviceRequestor) {
    if requestor == nil {
        panic("DeviceRequestor can't be nil.")
    }
    tid := asyncTasks.Alloc(nil, requestor)
    jsAdapterRequestDevice(tid, this.GetHandle(), 0)
}
type DeviceRequestor interface {
    OnRequested(device: *Device)
}

func Adapter.RequestDevice_Handler(handler: DeviceRequestHandler) {
    if handler == nil {
        panic("DeviceRequestHandler can't be nil.")
    }
    tid := asyncTasks.Alloc(nil, handler)
    jsAdapterRequestDevice(tid, this.GetHandle(), 0)
}
type DeviceRequestHandler func(device: *Device)

#wa:import webgpu adapter_request_device
func jsAdapterRequestDevice(tid: int, adapter: js.Handle, desc: js.Handle)

#wa:export gpu.onDeviceRequested
func onDeviceRequested(tid: int, dh: js.Handle) {
    _, handler := asyncTasks.Get(tid)
    device: *Device
    if dh != 0 {
        v: Device
        v.ExtObj = js.WrapExtObj(dh)
        device = &v
    }

    switch h := handler.(type) {
    case DeviceRequestor:
        h.OnRequested(device)

    case DeviceRequestHandler:
        h(device)
    }

    asyncTasks.Free(tid)
}

//---------------------------------------------------------------