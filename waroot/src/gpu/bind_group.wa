
import "js"

// 不能使用该类型直接声明值，需通过 Device.CreateBindGroup() 创建
type BindGroup struct {
    js.ExtObj
}

//---------------------------------------------------------------

// BindGroup 描述符，用于创建 BindGroup
type BindGroupDesc struct {
    Entries: []BindGroupEntry
    Label: string
    Layout: *BindGroupLayout
}

func BindGroupDesc.createExtObj() => js.ExtObj {
    d := js.NewExtObj()
    va := js.NewExtArray()
    for _, e := range this.Entries {
        va.Append_obj(e.ExtObj)
    }
    d.SetMember_obj("entries", va.ExtObj)

    d.SetMember_string("label", this.Label)

    d.SetMember_obj("layout", this.Layout.ExtObj)
    return d
}

//---------------------------------------------------------------

// 不能使用该类型直接声明值，需通过 NewBindGroupEntry() 创建
type BindGroupEntry struct {
    js.ExtObj
}

#wa:generic NewBindGroupEntry_TextureView NewBindGroupEntry_Sampler
func NewBindGroupEntry(binding: i32, b: BufferBinding) => BindGroupEntry {
    e: BindGroupEntry
    e.ExtObj = js.NewExtObj()
    e.SetMember_i32("binding", binding)
    e.SetMember_obj("resource", b.ExtObj)
    return e
}

func NewBindGroupEntry_TextureView(binding: i32, t: TextureView) => BindGroupEntry {
    e: BindGroupEntry
    e.ExtObj = js.NewExtObj()
    e.SetMember_i32("binding", binding)
    e.SetMember_obj("resource", t.ExtObj)
    return e
}

func NewBindGroupEntry_Sampler(binding: i32, s: Sampler) => BindGroupEntry {
    e: BindGroupEntry
    e.ExtObj = js.NewExtObj()
    e.SetMember_i32("binding", binding)
    e.SetMember_obj("resource", s.ExtObj)
    return e
}

//---------------------------------------------------------------

// 不能使用该类型直接声明值，需通过 NewBufferBinding() 创建
type BufferBinding struct {
    js.ExtObj
}

#wa:generic NewBufferBinding_OffsetSize
func NewBufferBinding(buffer: *Buffer) => BufferBinding {
    b: BufferBinding
    b.ExtObj = js.NewExtObj()
    b.SetMember_obj("buffer", buffer.ExtObj)
    return b
}

func NewBufferBinding_OffsetSize(buffer: *Buffer, offset, size: i32) => BufferBinding {
    b: BufferBinding
    b.ExtObj = js.NewExtObj()
    b.SetMember_obj("buffer", buffer.ExtObj)
    b.SetMember_i32("offset", offset)
    b.SetMember_i32("size", size)
    return b
}
